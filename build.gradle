plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id "edu.sc.seis.macAppBundle" version "2.3.0"
}

version='1.0'
group='excelsiorg'
applicationName='Nausicaa'
mainClassName='org.excelsi.nausicaa.Nausicaa'

repositories {
    //jcenter()
    mavenCentral()
    flatDir {
        dirs '/Users/jkw/.mu/lib'
    }
}

dependencies {
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'excelsiorg:solace:1.0'
    implementation 'excelsiorg:gimmal:1.0'
    implementation 'groovy:groovy-all:1.8.9'
    implementation 'imgscalr:imgscalr-lib:4.2'
    implementation 'Fxyz:Fxyz-Core:1.0'
    implementation 'Fxyz:Fxyz-lib:1.0'
    implementation 'google:gson:2.8.5'
    implementation 'apache:log4j:1.2.15'
    implementation 'asomov:snakeyaml:1.21'
    implementation 'slf4j:slf4j-api:1.7.7'
    implementation 'slf4j:slf4j-log4j12:1.7.7'
    implementation 'apache:log4j:1.2.15'

    testImplementation 'junit:junit:4.12'
}

test {
    useJUnit()

    maxHeapSize = '1G'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main']
        }
        resources {
            srcDirs = ['src/resources','src/etc']
        }
    }
    test {
        java {
            srcDirs = ['src/test']
        }
        groovy {
            srcDirs = ['src/test']
        }
    }
}

application {
    applicationDefaultJvmArgs = ["-Xmx12g","-Dapple.laf.useScreenMenuBar=true","-XX:-OmitStackTraceInFastThrow","-Dapp.root=HACK_FOR_GRADLE"]
    startScripts {
        doLast {
            unixScript.text = unixScript.text.replace('HACK_FOR_GRADLE', '\$APP_HOME')
            unixScript.text = unixScript.text.replace('-classpath','\$GRADLE_OPTS -classpath')
            windowsScript.text = windowsScript.text.replace('HACK_FOR_GRADLE', '%~dp0..')
        }
    }
}

distributions {
    main {
        baseName='Nausicaa'
        contents {
        }
    }
}

applicationDistribution.from("src/etc/") {
    into "etc"
}

macAppBundle {
    appName='Nausicaa'
    mainClassName = project.mainClassName
    // makeicns -in src/resources/na1_1024.png -out src/resources/nausicaa.icns
    icon = 'src/resources/nausicaa.icns'
    bundleJRE = false
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
    appStyle='Oracle'
}

task fixupDist(type: Copy) {
    from("$buildDir/install/Nausicaa/lib/") {
        into("${project.macAppBundle.jarSubdir}")
    }
    from("$buildDir/install/Nausicaa/etc/") {
        into("etc")
    }
    into("${project.buildDir}/${project.macAppBundle.appOutputDir}/${project.macAppBundle.appName}.app/Contents/")
}

project.tasks.getByName("createApp").dependsOn(project.tasks.getByName("installDist"))
project.tasks.getByName("fixupDist").dependsOn(project.tasks.getByName("createApp"))
project.tasks.getByName("fixupDist").dependsOn(project.tasks.getByName("installDist"))
project.tasks.getByName("createDmg").dependsOn(project.tasks.getByName("fixupDist"))
